plugins {
	id 'java'
	id 'org.springframework.boot' apply false
	id 'io.spring.dependency-management' apply false
	id 'com.google.cloud.tools.jib' apply false
	id 'checkstyle'
	id 'jacoco'
}

allprojects {
	repositories {
		mavenCentral()
	}

	apply plugin: 'java'
	apply plugin: 'checkstyle'

	checkstyle {
		toolVersion '10.13.0'
		configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
		maxWarnings = 0
		ignoreFailures = false
	}
}

subprojects {
	group = 'org.ideation'

	if (project.hasProperty('projVersion')) {
		project.version = project.projVersion
	} else {
		project.version = '0.0.1-SNAPSHOT'
	}

	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.google.cloud.tools.jib'
	apply plugin: 'jacoco'

	java {
		sourceCompatibility = JavaVersion.VERSION_21
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
	}

	test {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test
		reports {
			xml.required = true
			xml.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/xml/jacocoTestReport.xml').get().asFile
			csv.required = true
			csv.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/csv/report.csv').get().asFile
		}
	}

	jacocoTestCoverageVerification {
		dependsOn jacocoTestReport
		violationRules {
			rule {
				limit {
					minimum = 0.90
					counter = 'LINE'
				}
			}
		}
	}

	jib {
		to {
			image = "${group}/${project.name}:${version}"
		}
		from {
			image = "eclipse-temurin:21-jre"
		}
	}

}
